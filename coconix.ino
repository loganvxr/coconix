#include <Arduino.h>
#include <string.h>
#include <avr/pgmspace.h>

// Change these to reflect your hardware configuration! If a feature was enabled here but isn't supported for the hardware configuration in use, it will be disabled at build-time.
#define NO_MEMORY_CHECK 0      // Disable checking the amount of free memory available.
#define NO_SOFT_RESET 0        // Disable software resets.
#define NO_TONE_FUNC 0         // Disable piezo support.
#define NO_EEPROM 1            // Disable all access and usage of the EEPROM storage.
#define HW_NAME "Arduino UNO R4 WiFi"  // The name of the board running Coconix.
#define BAUD_RATE 115200       // On some boards, this may need to be reconfigured to prevent garbling in the Serial Monitor.
#define VERSION_NUMBER "0.1"   // Version string for this version of Coconix.
#define NO_WIFI 0              // Disable wifi support.
#define NO_BLE 0              // Disable Bluetooth Low Energy support.


#if not defined(ADAFRUIT_METRO_M0_EXPRESS) && not defined(ARDUINO_SAM_DUE) && not defined(ARDUINO_GIGA) && NO_EEPROM == 0  // EEPROM is allowed and not using Adafruit Metro M0 Express board.
#include <EEPROM.h>
#elif defined(ADAFRUIT_METRO_M0_EXPRESS) || defined(ARDUINO_SAM_DUE) || defined(ARDUINO_GIGA)
#undef NO_EEPROM
#define NO_EEPROM 1  // EEPROM cannot be used on some boards.
#endif

#ifdef ARDUINO_SAM_DUE
#undef NO_TONE_FUNC
#define NO_TONE_FUNC 1  // *sigh* Arduino Due boards are the worst.
#endif

#ifdef ARDUINO_GIGA
#undef NO_MEMORY_CHECK
#define NO_MEMORY_CHECK 1
#endif

#define MAX_FILES 10
#define NAME_LEN 12
#define CONTENT_LEN 32
#define PATH_LEN 16
#define DMESG_LINES 6
#define DMESG_LEN 40
#define EEPROM_MAGIC 0xAB
#define EEPROM_ADDR 0

#ifdef __arm__
// should use uinstd.h to define sbrk but Due causes a conflict
extern "C" char* sbrk(int incr);
#else   // __ARM__
extern char* __brkval;
#endif  // __arm__

#ifdef __arm__
#define HW_ARCH "ARM"

#elif defined(__AVR__)
#define HW_ARCH "AVR"

#else
#define HW_ARCH "Unknown"

#endif

typedef struct {
  char name[NAME_LEN];
  char content[CONTENT_LEN];
  char parentDir[PATH_LEN];
  int isDirectory;
  int active;
} RAMFile;

typedef struct {
  unsigned long timestamp;
  char message[DMESG_LEN];
} DmesgEntry;

RAMFile fs[MAX_FILES];
char currentPath[PATH_LEN] = "/";
char inputBuffer[32] = "";
int inputLen = 0;
DmesgEntry dmesg[DMESG_LINES];
int dmesgIndex = 0;

int freeMemory() {
  char top;

#if NO_MEMORY_CHECK == 1
  return -1;
#elif defined(__arm__)
  return &top - reinterpret_cast<char*>(sbrk(0));
#elif defined(CORE_TEENSY) || (ARDUINO > 103 && ARDUINO != 151)
  return &top - __brkval;
#else   // __arm__
  return __brkval ? &top - __brkval : &top - __malloc_heap_start;
#endif  // __arm__
}

bool is_argstr_empty(char* args) {
  return strcmp("", args) == 0;
}

#if defined(__AVR__) && NO_SOFT_RESET == 0
void (*resetFunc)(void) = 0;

#elif defined(__arm__) && NO_SOFT_RESET == 0
void resetFunc() {
  NVIC_SystemReset();
}

#elif NO_SOFT_RESET != 0
void resetFunc() {
  Serial.println(F("This build of Coconix has been configured to disable software resets."));
}

#else
void resetFunc() {
  Serial.println(F("Coconix doesn't support software resets on this hardware."));
}

#endif

// OPT
void addDmesg(const __FlashStringHelper* msg) {
  if (dmesgIndex >= DMESG_LINES) dmesgIndex = 0;
  dmesg[dmesgIndex].timestamp = millis() / 1000;
  strncpy_P(dmesg[dmesgIndex].message, (PGM_P)msg, DMESG_LEN - 1);
  dmesg[dmesgIndex].message[DMESG_LEN - 1] = '\0';
  dmesgIndex++;
}

void addDmesgRam(const char* msg) {
  if (dmesgIndex >= DMESG_LINES) dmesgIndex = 0;
  dmesg[dmesgIndex].timestamp = millis() / 1000;
  strncpy(dmesg[dmesgIndex].message, msg, DMESG_LEN - 1);
  dmesg[dmesgIndex].message[DMESG_LEN - 1] = '\0';
  dmesgIndex++;
}

void saveFS() {
#if NO_EEPROM == 1
  Serial.println(F("Filesystem could not be synced because EEPROM writes are disabled."));
#else
  EEPROM.update(EEPROM_ADDR, EEPROM_MAGIC);
  int addr = EEPROM_ADDR + 1;
  for (int i = 0; i < MAX_FILES; i++) {
    EEPROM.put(addr, fs[i]);
    addr += sizeof(RAMFile);
  }
  Serial.println(F("Synced to EEPROM."));
  addDmesg(F("FS saved to EEPROM"));
#endif
}

#if NO_EEPROM == 0
void loadFS() {
  if (EEPROM.read(EEPROM_ADDR) != EEPROM_MAGIC) return;
  int addr = EEPROM_ADDR + 1;
  for (int i = 0; i < MAX_FILES; i++) {
    EEPROM.get(addr, fs[i]);
    addr += sizeof(RAMFile);
  }
  addDmesg(F("FS loaded from EEPROM"));
}
#endif

#if NO_WIFI == 0
#include <WiFi.h>
#endif

void initFS() {
#if NO_EEPROM == 0
  // If saved filesystem exists, load it instead of defaults
  if (EEPROM.read(EEPROM_ADDR) == EEPROM_MAGIC) {
    loadFS();
    return;
  }
#endif

  int d, i;
  const char* dirs[] = { "home", "dev" };
  for (d = 0; d < 2; d++) {
    for (i = 0; i < MAX_FILES; i++) {
      if (!fs[i].active) {
        strncpy(fs[i].name, dirs[d], NAME_LEN - 1);
        fs[i].name[NAME_LEN - 1] = '\0';
        strncpy(fs[i].parentDir, "/", PATH_LEN - 1);
        fs[i].parentDir[PATH_LEN - 1] = '\0';
        fs[i].isDirectory = 1;
        fs[i].active = 1;
        break;
      }
    }
  }

  char devPath[PATH_LEN] = "/dev/";
  const char* pins[] = { "pin2", "pin3", "pin4" };
  for (d = 0; d < 3; d++) {
    for (i = 0; i < MAX_FILES; i++) {
      if (!fs[i].active) {
        strncpy(fs[i].name, pins[d], NAME_LEN - 1);
        fs[i].name[NAME_LEN - 1] = '\0';
        strncpy(fs[i].parentDir, devPath, PATH_LEN - 1);
        fs[i].parentDir[PATH_LEN - 1] = '\0';
        fs[i].isDirectory = 0;
        fs[i].content[0] = '\0';
        fs[i].active = 1;
        break;
      }
    }
  }

  // OPT
  addDmesg(F("Kernel initialized"));
  addDmesg(F("Filesystem mounted"));
  addDmesg(F("Ready for commands"));
}

void printPrompt() {
  Serial.print(F("root@arduino:"));
  Serial.print(currentPath);
  Serial.print(F("# "));
}

void setup() {
  Serial.begin(BAUD_RATE);
  initFS();
  delay(1000);
  Serial.print(F("--- Coconix v"));
  Serial.print(VERSION_NUMBER);
  Serial.println(F(" ---"));
  Serial.println(F("Type 'help' for commands"));
  printPrompt();
}

void generate_tone(int pin, int freq) {
#if NO_TONE_FUNC == 0
  tone(pin, freq);
#elif NO_TONE_FUNC == 1
  Serial.println(F("This build of Coconix has been configured to disable piezo support."));
#endif
}

void stop_tone(int pin) {
#if NO_TONE_FUNC == 0
  noTone(pin);
#elif NO_TONE_FUNC == 1
  Serial.println(F("This build of Coconix has been configured to disable piezo support."));
#endif
}

void clear_eeprom() {
#if NO_EEPROM == 0
  for (int i = 0; i < EEPROM.length(); i++) {
    Serial.println(String("Clearing byte #") + String(i));
    EEPROM.update(i, 255);
  }
#else
  Serial.println(F("Filesystem could not be cleared because EEPROM writes are disabled."));
#endif
}

void loop() {
  if (Serial.available() > 0) {
    char c = Serial.read();
    if (c == '\r' || c == '\n') {
      if (inputLen > 0) {
        inputBuffer[inputLen] = '\0';
        Serial.println();
        executeCommand(inputBuffer);
        inputLen = 0;
        memset(inputBuffer, 0, 32);
        printPrompt();
      } else {

        Serial.println();
        printPrompt();
      }
    } else if (c == 8 || c == 127) {
      if (inputLen > 0) {
        inputLen--;
        inputBuffer[inputLen] = '\0';
        Serial.print(F("\b \b"));
      }
    } else if (inputLen < 31) {
      Serial.print(c);
      inputBuffer[inputLen] = c;
      inputLen++;
    }
  }
}

int indexOf(const char* str, const char* substr) {
  int i, j, slen = strlen(str), sublen = strlen(substr);
  for (i = 0; i <= slen - sublen; i++) {
    int match = 1;
    for (j = 0; j < sublen; j++) {
      if (str[i + j] != substr[j]) {
        match = 0;
        break;
      }
    }
    if (match) return i;
  }
  return -1;
}

int atoi_safe(const char* str) {
  int num = 0;
  while (*str >= '0' && *str <= '9') {
    num = num * 10 + (*str - '0');
    str++;
  }
  return num;
}

void toLowercase(char* str) {
  int i;
  for (i = 0; str[i] != '\0'; i++) {
    if (str[i] >= 'A' && str[i] <= 'Z') str[i] = str[i] - 'A' + 'a';
  }
}

int safeConcatPath(char* dest, const char* add) {
  int destLen = strlen(dest);
  int addLen = strlen(add);
  if (destLen + addLen + 2 >= PATH_LEN) return 0;
  strncat(dest, add, PATH_LEN - destLen - 1);
  strncat(dest, "/", PATH_LEN - strlen(dest) - 1);
  return 1;
}

void runScript(const char* content);

void executeCommand(char* line) {
  char cmd[32] = "";
  char args[32] = "";
  int space1 = -1;
  int i, sp, pin, count;
  char buf[40];

  strncpy(cmd, line, 31);
  cmd[31] = '\0';

  for (i = 0; cmd[i] != '\0'; i++) {
    if (cmd[i] == ' ') {
      space1 = i;
      strncpy(args, cmd + i + 1, 31);
      args[31] = '\0';
      cmd[i] = '\0';
      break;
    }
  }

  toLowercase(cmd);

  // OPT
  if (strcmp_P(cmd, PSTR("pinmode")) == 0) {
    sp = indexOf(args, " ");
    if (sp == -1) {
      Serial.println(F("Usage: pinmode [pin] [in/out]"));
      return;
    }
    pin = atoi_safe(args);
    char mode[8] = "";
    strncpy(mode, args + sp + 1, 7);
    mode[7] = '\0';
    toLowercase(mode);
    if (strcmp_P(mode, PSTR("out")) == 0) {
      pinMode(pin, OUTPUT);
      snprintf_P(buf, sizeof(buf), PSTR("Pin %d set to OUTPUT"), pin);
      addDmesgRam(buf);
      Serial.println(F("Pin set to OUTPUT"));
    } else if (strcmp_P(mode, PSTR("in")) == 0) {
      pinMode(pin, INPUT_PULLUP);
      snprintf_P(buf, sizeof(buf), PSTR("Pin %d set to INPUT"), pin);
      addDmesgRam(buf);
      Serial.println(F("Pin set to INPUT_PULLUP"));
    }
  } else if (strcmp_P(cmd, PSTR("write")) == 0) {
    sp = indexOf(args, " ");
    if (sp == -1) {
      Serial.println(F("Usage: write [pin] [high/low]"));
      return;
    }
    pin = atoi_safe(args);
    char val[8] = "";
    strncpy(val, args + sp + 1, 7);
    val[7] = '\0';
    toLowercase(val);
    digitalWrite(pin, (strcmp_P(val, PSTR("high")) == 0 ? HIGH : LOW));
    snprintf_P(buf, sizeof(buf), PSTR("Pin %d wrote %s"), pin, strcmp_P(val, PSTR("high")) == 0 ? "HIGH" : "LOW");
    addDmesgRam(buf);
    Serial.println(F("Write OK."));
  } else if (strcmp_P(cmd, PSTR("read")) == 0) {
    pin = atoi_safe(args);
    int value = digitalRead(pin);
    Serial.print(F("Pin "));
    Serial.print(pin);
    Serial.print(F(" value: "));
    Serial.println(value);
    snprintf_P(buf, sizeof(buf), PSTR("Pin %d read: %d"), pin, value);
    addDmesgRam(buf);
  } else if (strcmp_P(cmd, PSTR("gpio")) == 0) {
    sp = indexOf(args, " ");
    if (sp == -1) {
      Serial.println(F("Usage: gpio [pin] [on/off] OR gpio vixa [count]"));
      return;
    }
    char pinStr[8] = "";
    strncpy(pinStr, args, sp);
    pinStr[sp] = '\0';
    char action[8] = "";
    strncpy(action, args + sp + 1, 7);
    action[7] = '\0';
    toLowercase(action);

    if (strcmp_P(pinStr, PSTR("vixa")) == 0) {
      count = atoi_safe(action);
      if (count <= 0) count = 10;
      addDmesg(F("LED disco mode activated"));
      Serial.println(F("LED DISCO MODE!"));
      int cycle, p;
      for (cycle = 0; cycle < count; cycle++) {
        for (p = 2; p <= 13; p++) {
          pinMode(p, OUTPUT);
          digitalWrite(p, HIGH);
          delay(50);
          digitalWrite(p, LOW);
        }
      }
      Serial.println(F("Disco finished!"));
      addDmesg(F("Disco complete"));
    } else {
      pin = atoi_safe(pinStr);
      if (strcmp_P(action, PSTR("on")) == 0) {
        pinMode(pin, OUTPUT);
        digitalWrite(pin, HIGH);
        snprintf_P(buf, sizeof(buf), PSTR("GPIO %d ON"), pin);
        addDmesgRam(buf);
        Serial.print(F("GPIO "));
        Serial.print(pin);
        Serial.println(F(" ON"));
      } else if (strcmp_P(action, PSTR("off")) == 0) {
        pinMode(pin, OUTPUT);
        digitalWrite(pin, LOW);
        snprintf_P(buf, sizeof(buf), PSTR("GPIO %d OFF"), pin);
        addDmesgRam(buf);
        Serial.print(F("GPIO "));
        Serial.print(pin);
        Serial.println(F(" OFF"));
      } else if (strcmp_P(action, PSTR("toggle")) == 0) {
        pinMode(pin, OUTPUT);
        digitalWrite(pin, !digitalRead(pin));
        snprintf_P(buf, sizeof(buf), PSTR("GPIO %d toggled"), pin);
        addDmesgRam(buf);
        Serial.print(F("GPIO "));
        Serial.print(pin);
        Serial.println(F(" toggled"));
      }
    }
  } else if (strcmp_P(cmd, PSTR("ls")) == 0) {
    int empty = 1, j;
    for (j = 0; j < MAX_FILES; j++) {
      if (fs[j].active && strcmp(fs[j].parentDir, currentPath) == 0) {
        Serial.print(fs[j].name);
        if (fs[j].isDirectory) Serial.print(F("/"));
        Serial.print(F("  "));
        empty = 0;
      }
    }
    if (empty) Serial.print(F("(empty)"));
    Serial.println();
  } else if (strcmp_P(cmd, PSTR("mkdir")) == 0 || strcmp_P(cmd, PSTR("touch")) == 0) {
    int foundSlot = -1, j;
    for (j = 0; j < MAX_FILES; j++) {
      if (!fs[j].active) {
        foundSlot = j;
        break;
      }
    }
    if (foundSlot == -1) {
      Serial.println(F("No space."));
      return;
    }
    strncpy(fs[foundSlot].name, args, NAME_LEN - 1);
    fs[foundSlot].name[NAME_LEN - 1] = '\0';
    strncpy(fs[foundSlot].parentDir, currentPath, PATH_LEN - 1);
    fs[foundSlot].parentDir[PATH_LEN - 1] = '\0';
    fs[foundSlot].isDirectory = (strcmp_P(cmd, PSTR("mkdir")) == 0);
    fs[foundSlot].content[0] = '\0';
    fs[foundSlot].active = 1;
    Serial.println(F("OK."));
  } else if (strcmp_P(cmd, PSTR("cd")) == 0) {
    if (strcmp_P(args, PSTR("..")) == 0 || strcmp_P(args, PSTR("/")) == 0) {
      strncpy(currentPath, "/", PATH_LEN - 1);
      currentPath[PATH_LEN - 1] = '\0';
    } else {
      int j, found = 0;
      for (j = 0; j < MAX_FILES; j++) {
        if (fs[j].active && fs[j].isDirectory && strcmp(args, fs[j].name) == 0 && strcmp(fs[j].parentDir, currentPath) == 0) {
          if (!safeConcatPath(currentPath, fs[j].name)) {
            strncpy(currentPath, "/", PATH_LEN - 1);
            currentPath[PATH_LEN - 1] = '\0';
            Serial.println(F("Path too long."));
            return;
          }
          found = 1;
          break;
        }
      }
      if (!found) Serial.println(F("No dir."));
    }
  } else if (strcmp_P(cmd, PSTR("pwd")) == 0) {
    Serial.println(currentPath);
  } else if (strcmp_P(cmd, PSTR("echo")) == 0) {
    int arrow = indexOf(args, " > ");
    if (arrow != -1) {
      char text[40] = "";
      strncpy(text, args, arrow);
      text[arrow] = '\0';
      char filename[12] = "";
      strncpy(filename, args + arrow + 3, NAME_LEN - 1);
      filename[NAME_LEN - 1] = '\0';
      int j, found = 0;
      for (j = 0; j < MAX_FILES; j++) {
        if (fs[j].active && !fs[j].isDirectory && strcmp(filename, fs[j].name) == 0 && strcmp(fs[j].parentDir, currentPath) == 0) {
          strncpy(fs[j].content, text, CONTENT_LEN - 1);
          fs[j].content[CONTENT_LEN - 1] = '\0';
          Serial.println(F("Saved."));
          if (strcmp_P(fs[j].parentDir, PSTR("/dev/")) == 0 && strncmp_P(fs[j].name, PSTR("pin"), 3) == 0) {
            int devPin = atoi_safe(fs[j].name + 3);
            if (devPin > 0) {
              pinMode(devPin, OUTPUT);
              digitalWrite(devPin, (text[0] == '1') ? HIGH : LOW);
              snprintf_P(buf, sizeof(buf), PSTR("GPIO %d %s via echo"), devPin, (text[0] == '1') ? "HIGH" : "LOW");
              addDmesgRam(buf);
            }
          }
          found = 1;
          break;
        }
      }
      if (!found) Serial.println(F("File not found."));
    } else {
      Serial.println(args);
    }
  } else if (strcmp_P(cmd, PSTR("cat")) == 0) {
    int j, found = 0;
    for (j = 0; j < MAX_FILES; j++) {
      if (fs[j].active && !fs[j].isDirectory && strcmp(args, fs[j].name) == 0 && strcmp(fs[j].parentDir, currentPath) == 0) {
        Serial.println(fs[j].content);
        found = 1;
        break;
      }
    }
    if (!found) Serial.println(F("File not found."));
  } else if (strcmp_P(cmd, PSTR("info")) == 0) {
    int j, found = 0;
    for (j = 0; j < MAX_FILES; j++) {
      if (fs[j].active && strcmp(args, fs[j].name) == 0 && strcmp(fs[j].parentDir, currentPath) == 0) {
        Serial.print(F("Name: "));
        Serial.println(fs[j].name);
        Serial.print(F("Type: "));
        Serial.println(fs[j].isDirectory ? F("Directory") : F("File"));
        Serial.print(F("Size: "));
        Serial.print(strlen(fs[j].content));
        Serial.println(F(" bytes"));
        found = 1;
        break;
      }
    }
    if (!found) Serial.println(F("Not found."));
  } else if (strcmp_P(cmd, PSTR("rm")) == 0) {
    int j, found = 0;
    for (j = 0; j < MAX_FILES; j++) {
      if (fs[j].active && strcmp(args, fs[j].name) == 0 && strcmp(fs[j].parentDir, currentPath) == 0) {
        if (fs[j].isDirectory) {
          char dirPath[PATH_LEN];
          snprintf_P(dirPath, PATH_LEN, PSTR("%s%s/"), currentPath, args);
          int k;
          for (k = 0; k < MAX_FILES; k++) {
            if (fs[k].active && strncmp(fs[k].parentDir, dirPath, strlen(dirPath)) == 0) {
              fs[k].active = 0;
            }
          }
        }
        fs[j].active = 0;
        Serial.println(F("Removed."));
        found = 1;
        break;
      }
    }
    if (!found) Serial.println(F("Not found."));
  } else if (strcmp_P(cmd, PSTR("dmesg")) == 0) {
    Serial.println(F("=== KERNEL MESSAGES ==="));
    int j;
    for (j = 0; j < DMESG_LINES; j++) {
      if (dmesg[j].message[0] != '\0') {
        Serial.print(F("["));
        Serial.print(dmesg[j].timestamp);
        Serial.print(F("] "));
        Serial.println(dmesg[j].message);
      }
    }
  } else if (strcmp_P(cmd, PSTR("uptime")) == 0) {
    unsigned long s = millis() / 1000;
    unsigned long h = s / 3600;
    unsigned long m = (s % 3600) / 60;
    unsigned long sec = s % 60;
    Serial.print(F("up "));
    Serial.print(h);
    Serial.print(F("h "));
    Serial.print(m);
    Serial.print(F("m "));
    Serial.print(sec);
    Serial.println(F("s"));
    addDmesg(F("uptime command"));
  } else if (strcmp_P(cmd, PSTR("df")) == 0 || strcmp_P(cmd, PSTR("free")) == 0) {
    Serial.print(F("Free RAM: "));
    Serial.print(freeMemory());
    Serial.println(F(" bytes"));
  } else if (strcmp_P(cmd, PSTR("whoami")) == 0) {
    Serial.println(F("root"));
  } else if (strcmp_P(cmd, PSTR("uname")) == 0) {
    Serial.println(F("Coconix v1.0"));
    Serial.print(F("Architecture: "));
    Serial.println(HW_ARCH);
    Serial.print(F("Hardware: "));
    Serial.println(HW_NAME);
    Serial.print(F("RAM: "));
    Serial.print(freeMemory());
    Serial.println(F(" bytes free"));
  } else if (strcmp_P(cmd, PSTR("reboot")) == 0) {
    Serial.println(F("Rebooting..."));
    addDmesg(F("System reboot"));
    saveFS();
    delay(500);
    resetFunc();
  } else if (strcmp_P(cmd, PSTR("clear")) == 0) {
    int j;
    for (j = 0; j < 30; j++) Serial.println();
  } else if (strcmp_P(cmd, PSTR("sh")) == 0) {
    if (args[0] == '\0') {
      Serial.println(F("Usage: sh [script]"));
      return;
    }
    int j, found = 0;
    for (j = 0; j < MAX_FILES; j++) {
      if (fs[j].active && !fs[j].isDirectory && strcmp(args, fs[j].name) == 0 && strcmp(fs[j].parentDir, currentPath) == 0) {
        found = 1;
        addDmesg(F("sh: running script"));
        runScript(fs[j].content);
        break;
      }
    }
    if (!found) Serial.println(F("Script not found."));
  } else if (strcmp_P(cmd, PSTR("help")) == 0) {
    Serial.println(F("Commands: ls, cd, pwd, mkdir, touch, cat, echo, rm, info"));
    Serial.println(F("          pinmode, write, read, gpio, sh"));
    Serial.println(F("          uptime, uname, dmesg, df, free, whoami, clear, reboot, tone, notone, sleep"));
    Serial.println(F("          sync, build-info"));
    Serial.println(F("GPIO: gpio [pin] on/off/toggle  |  gpio vixa [count] | tone [pin] [freq] | notone [pin]"));
    Serial.println(F("SH:   sh [file]  -- run script (use ; as line separator)"));

  } else if (strcmp_P(cmd, PSTR("tone")) == 0) {
    int pin, freq;
    sp = indexOf(args, " ");
    if (sp != -1) {
      freq = atoi_safe(args + sp + 1);
      sp = indexOf(args + sp, " ");
      pin = atoi_safe(args + sp);
      generate_tone(pin, freq);
    }

    else {
      Serial.println(F("Usage: tone [pin] [freq]"));
    }

  }

  else if (strcmp_P(cmd, PSTR("reset-fs")) == 0) {
#if NO_EEPROM == 0
    Serial.println(F("Clearing the filesystem! This may take several minutes."));
    clear_eeprom();
    Serial.println(F("Rebooting!"));
    resetFunc();
#else
    Serial.println(F("Clearing the EEPROM data is disabled in this build."));
#endif
  }

  else if (strcmp_P(cmd, PSTR("notone")) == 0) {
    int pin;
    if (!is_argstr_empty(args)) {
      pin = atoi_safe(args);
      stop_tone(pin);
    }

    else {
      Serial.println(F("Usage: notone [pin]"));
    }
  } else if (strcmp_P(cmd, PSTR("help")) == 0) {
    Serial.println(F("Commands: ls, cd, pwd, mkdir, touch, cat, echo, rm, info"));
    Serial.println(F("          pinmode, write, read, gpio, pwm, sh"));
    Serial.println(F("          uptime, uname, dmesg, df, free, whoami, clear, reboot"));
    Serial.println(F("GPIO: gpio [pin] on/off/toggle  |  gpio vixa [count]"));
    Serial.println(F("SH:   sh [file]  -- run script (use ; as line separator)"));
    Serial.println(F("SYNC: sync       -- save filesystem to EEPROM"));
  }

  else if (strcmp_P(cmd, PSTR("sleep")) == 0) {
    int time;
    if (!is_argstr_empty(args)) {
      time = atoi_safe(args);
      delay(time * 1000);
    }

    else {
      Serial.println(F("Usage: sleep [time]"));
    }

  }

  else if (strcmp_P(cmd, PSTR("sync")) == 0) {
    saveFS();
  }

  else if (strcmp_P(cmd, PSTR("build-info")) == 0) {
    Serial.println(String("NO_MEMORY_CHECK: ") + NO_MEMORY_CHECK);
    Serial.println(String("NO_SOFT_RESET: ") + NO_SOFT_RESET);
    Serial.println(String("NO_TONE_FUNC: ") + NO_TONE_FUNC);
    Serial.println(String("NO_EEPROM: ") + NO_EEPROM);
    Serial.println(String("NO_WIFI: ") + NO_WIFI);
    Serial.println(String("HW_NAME: ") + HW_NAME);
    Serial.println(String("HW_ARCH: ") + HW_ARCH);
  }

  else if (strcmp_P(cmd, PSTR("wifi")) == 0) {
    #if NO_WIFI == 0
    if (is_argstr_empty(args)) {
      Serial.println(F("wifi [status/connect/disconnect]"));
      return;
    }

    // tokenize additional arugments
    char wifiArgs[32];
    strncpy(wifiArgs, args, sizeof(wifiArgs) - 1);
    wifiArgs[sizeof(wifiArgs) - 1] = '\0';

    char* subcmd = strtok(wifiArgs, " ");
    char* arg1 = strtok(NULL, " ");
    char* arg2 = strtok(NULL, " ");

    // handle sub-commands
    if (strcmp_P(subcmd, PSTR("status")) == 0) {
      if (WiFi.status() == WL_CONNECTED) {
        Serial.print(F("Connected to: "));
        Serial.println(WiFi.SSID());

        Serial.print(F("IP: "));
        Serial.println(WiFi.localIP());

        Serial.print(F("Signal: "));
        Serial.print(WiFi.RSSI());
        Serial.println(F(" dBm"));
      } else {
        Serial.println(F("WiFi not connected!"));
      } // connect sub-command
    } else if (strcmp_P(subcmd, PSTR("connect")) == 0) {
      // check if ssid and pass are set in the command
      // need to add support for non password protected wifi in the future
      if (arg1 == NULL) {
        Serial.println(F("Usage: wifi connect [ssid] [password]"));
        return;
      }
      
      // check if wifi is already connected
      if (WiFi.status() == WL_CONNECTED) {
        Serial.println(F("Already connected."));
        return;
      }
      
      // time to actually connect to the wifi
      Serial.print(F("Connecting to "));
      Serial.print(arg1);

      // if there wasn't a password provided, just assume that the wifi has no password
      if (arg2 == NULL) {
        WiFi.begin(arg1);
      } else {
        WiFi.begin(arg1, arg2);
      }

      int attempts = 0;
      
      while (WiFi.status() != WL_CONNECTED && attempts < 20) {
        delay(500);
        Serial.print(F("."));
        attempts++;
      }

      Serial.println();

      if (WiFi.status() == WL_CONNECTED) {
        Serial.println(F("Connected!"));

        Serial.print(F("IP: "));
        Serial.println(WiFi.localIP());

        addDmesg(F("WiFi connected"));
      } else {
        Serial.println(F("Connection failed."));
      } // handle disconnect
    } else if (strcmp_P(subcmd, PSTR("disconnect")) == 0) {
      WiFi.disconnect();
      Serial.println(F("WiFi disconnected."));
      addDmesg(F("WiFi disconnected"));
    } else if (strcmp_P(subcmd, PSTR("ping")) == 0) { // handle ping commands

      // properly check if we're connected to wifi
      if (WiFi.status() != WL_CONNECTED) {
        Serial.println(F("WiFi not connected!"));
        return;
      }

      if (arg1 == NULL) {
        Serial.println(F("Usage: wifi ping [destination]"));
        return;
      }

      IPAddress ip;

      // check if the input is an ip
      if (!ip.fromString(arg1)) {

        // this is not an ip so we need to resolve the hostname
        if (!WiFi.hostByName(arg1, ip)) {
          Serial.println(F("DNS lookup failed"));
          return;
        }
      }

      // time to ping the ip but before we need to do some fancy formatting

      // output -> PING arg1 (IP) 32 bytes of data.
      Serial.print(F("PING "));
      Serial.print(arg1);
      Serial.print(F(" ("));
      Serial.print(ip.toString());
      Serial.print(F(") "));
      Serial.println(F("32 bytes of data."));
      Serial.println();

      // we're always going to transmit 4 packets for simplicity
      int received = 0;
      int packet_loss = 0;
      int total_time = 0; // total latency in ms
      int count = 0;

      // track total session duration
      unsigned long session_start = millis();

      while (count != 4) {

        WiFiClient client;

        // start timing the connection
        unsigned long start = millis();

        // attempt a tcp connection instead of icmp
        bool connected = client.connect(ip, 443);

        // calculate latency
        int result = millis() - start;

        if (connected) {

          // output -> Reply from arg1: bytes=32 time=[x]ms
          Serial.print(F("Reply from "));
          Serial.print(arg1);
          Serial.print(F(": bytes=32 time="));
          Serial.print(result);
          Serial.println(F("ms"));

          received++;

          total_time += result;

          // close the socket
          client.stop();

        } else {

          Serial.println(F("Ping failed"));
        }

        count++;

        // small delay between packets
        delay(250);
      }

      // calculate packet loss percentage
      packet_loss = ((count - received) * 100) / count;

      int avg_time = 0;

      // avoid division by zero
      if (received > 0) {
          avg_time = total_time / received;
      }

      // calculate total elapsed time
      unsigned long total_duration = millis() - session_start;

      // output -> --- arg1 ping statistics ---
      Serial.println();
      Serial.print(F("--- "));
      Serial.print(arg1);
      Serial.println(F(" ping statistics ---"));

      // output -> 4 packets transmitted, [x] received, [x]% packet loss, time [x]ms
      Serial.print(F("4 packets transmitted, "));
      Serial.print(received);
      Serial.print(F(" received, "));
      Serial.print(packet_loss);
      Serial.print(F("% packet loss, time "));
      Serial.print(total_duration);
      Serial.println(F("ms"));

      // output -> rtt avg = [x]ms
      if (received > 0) {
        Serial.print(F("rtt avg = "));
        Serial.print(avg_time);
        Serial.println(F("ms"));
      }
    } else { // handle unknown commands
      Serial.println(F("Unknown wifi subcommand."));
    }
    #else
    Serial.println(F("The WiFi module is not enaled in this build!"));
    #endif
  }

  else if (strcmp_P(cmd, PSTR("ble")) == 0) {
    #if NO_BLE == 0

    #else
      Serial.println(F("BLE is not enabled in this build!"));
    #endif
  }

  else {
    Serial.println(F("Unknown command."));
  }
}

// Interpreter sh
void runScript(const char* content) {
  char line[32];
  int ci = 0, li = 0, lineNum = 0;
  int len = strlen(content);

  while (ci <= len) {
    char c = (ci < len) ? content[ci] : ';';
    ci++;
    if (c == ';' || c == '\n' || c == '\r') {
      if (li > 0) {
        line[li] = '\0';
        lineNum++;
        Serial.print(F("[sh:"));
        Serial.print(lineNum);
        Serial.print(F("] "));
        Serial.println(line);
        executeCommand(line);
        li = 0;
      }
    } else {
      if (li < 31) line[li++] = c;
    }
  }
  addDmesg(F("sh: script done"));
  Serial.println(F("[sh] done."));
}